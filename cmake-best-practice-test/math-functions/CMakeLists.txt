## PROJECT SETTINGS ##
cmake_minimum_required(VERSION 3.15.0)
project(math_functions
    VERSION 0.1.0
    LANGUAGES CXX
)

## CXX SETTINGS ##
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED True)
set(CMAKE_DEBUG_POSTFIX -d)

## DEPENDENCIES CONFIGURATIONS ##
set(DEPENDENCIES_ROOT_DIR /home)
if(DEFINED ENV{DEPENDENCIES_ROOT_DIR})
    set(DEPENDENCIES_ROOT_DIR $ENV{DEPENDENCIES_ROOT_DIR})
endif()

set(MATH_TYPES_PACKAGE_NAME math_types)
set(MATH_TYPES_PACKAGE_VERSION 0.1.0)
set(MATH_TYPES_PACKAGE_CONFIG_DIR ${DEPENDENCIES_ROOT_DIR}/${MATH_TYPES_PACKAGE_NAME}/${CMAKE_SYSTEM_NAME}/${CMAKE_SYSTEM_VERSION}/${CMAKE_BUILD_TYPE}/cmake)

list(APPEND CMAKE_PREFIX_PATH ${MATH_TYPES_PACKAGE_CONFIG_DIR})

find_package(${MATH_TYPES_PACKAGE_NAME} ${MATH_TYPES_PACKAGE_VERSION} REQUIRED CONFIG)

## BUILD CONFIGURATIONS ##
set(MATH_FUNCTIONS math_functions)

file(GLOB_RECURSE sources
    ${CMAKE_SOURCE_DIR}/src/*.c
    ${CMAKE_SOURCE_DIR}/src/*.cxx
    ${CMAKE_SOURCE_DIR}/src/*.cpp
)
file(GLOB_RECURSE private_headers
    ${CMAKE_SOURCE_DIR}/src/*.h
    ${CMAKE_SOURCE_DIR}/src/*.hxx
    ${CMAKE_SOURCE_DIR}/src/*.hpp
)
file(GLOB_RECURSE public_headers
    ${CMAKE_SOURCE_DIR}/inc/*.h
    ${CMAKE_SOURCE_DIR}/inc/*.hxx
    ${CMAKE_SOURCE_DIR}/inc/*.hpp
)

add_library(${MATH_FUNCTIONS} STATIC ${sources})

target_include_directories(${MATH_FUNCTIONS}
    PRIVATE
        ${CMAKE_SOURCE_DIR}/src
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/inc>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

target_link_libraries(${MATH_FUNCTIONS}
    PUBLIC
        ${${MATH_TYPES_PACKAGE_NAME}_LIBRARIES}
)

## INTSTALL CONFIGURATIONS ##
include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

set(INSTALL_PREFIX_ROOT_DIR ${CMAKE_SOURCE_DIR}/install)
if(DEFINED ENV{INSTALL_PREFIX_ROOT_DIR})
    set(INSTALL_PREFIX_ROOT_DIR $ENV{INSTALL_PREFIX_ROOT_DIR})
endif()

set(CMAKE_INSTALL_PREFIX 
    ${INSTALL_PREFIX_ROOT_DIR}/${PROJECT_NAME}/${CMAKE_SYSTEM_NAME}/${CMAKE_SYSTEM_VERSION}/${CMAKE_BUILD_TYPE}
)

install(TARGETS ${MATH_FUNCTIONS}
    EXPORT ${MATH_FUNCTIONS}Targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

install(FILES ${public_headers} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

install(EXPORT ${MATH_FUNCTIONS}Targets
    FILE ${MATH_FUNCTIONS}Targets.cmake
    NAMESPACE ${CMAKE_PROJECT_NAME}::
    DESTINATION cmake
)

write_basic_package_version_file(
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_FUNCTIONS}ConfigVersion.cmake
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

configure_package_config_file(${CMAKE_SOURCE_DIR}/cmake/Config.cmake.in
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_FUNCTIONS}Config.cmake
    INSTALL_DESTINATION cmake
)

install(FILES
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_FUNCTIONS}Config.cmake
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_FUNCTIONS}ConfigVersion.cmake
    DESTINATION cmake
)
