## PROJECT SETTINGS ##
cmake_minimum_required(VERSION 3.15.0)
project(math_types
    VERSION 0.1.0
    LANGUAGES CXX
)

## CXX SETTINGS ##
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED True)
set(CMAKE_DEBUG_POSTFIX -d)

## DEPENDENCIES CONFIGURATIONS ##
set(DEPENDENCIES_ROOT_DIR /home)
if(DEFINED ENV{DEPENDENCIES_ROOT_DIR})
    set(DEPENDENCIES_ROOT_DIR $ENV{DEPENDENCIES_ROOT_DIR})
endif()

## BUILD CONFIGURATIONS ##
set(MATH_TYPES math_types)

file(GLOB_RECURSE sources
    ${CMAKE_SOURCE_DIR}/src/*.c
    ${CMAKE_SOURCE_DIR}/src/*.cxx
    ${CMAKE_SOURCE_DIR}/src/*.cpp
)
file(GLOB_RECURSE private_headers
    ${CMAKE_SOURCE_DIR}/src/*.h
    ${CMAKE_SOURCE_DIR}/src/*.hxx
    ${CMAKE_SOURCE_DIR}/src/*.hpp
)
file(GLOB_RECURSE public_headers
    ${CMAKE_SOURCE_DIR}/inc/*.h
    ${CMAKE_SOURCE_DIR}/inc/*.hxx
    ${CMAKE_SOURCE_DIR}/inc/*.hpp
)

add_library(${MATH_TYPES} STATIC ${sources})

target_include_directories(${MATH_TYPES}
    PRIVATE
        ${CMAKE_SOURCE_DIR}/src
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/inc>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

## INTSTALL CONFIGURATIONS ##
include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

set(INSTALL_PREFIX_ROOT_DIR ${CMAKE_SOURCE_DIR}/install)
if(DEFINED ENV{INSTALL_PREFIX_ROOT_DIR})
    set(INSTALL_PREFIX_ROOT_DIR $ENV{INSTALL_PREFIX_ROOT_DIR})
endif()

set(CMAKE_INSTALL_PREFIX 
    ${INSTALL_PREFIX_ROOT_DIR}/${PROJECT_NAME}/${CMAKE_SYSTEM_NAME}/${CMAKE_SYSTEM_VERSION}/${CMAKE_BUILD_TYPE}
)

install(TARGETS ${MATH_TYPES}
    EXPORT ${MATH_TYPES}Targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

install(FILES ${public_headers} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

install(EXPORT ${MATH_TYPES}Targets
    FILE ${MATH_TYPES}Targets.cmake
    NAMESPACE ${CMAKE_PROJECT_NAME}::
    DESTINATION cmake
)

write_basic_package_version_file(
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_TYPES}ConfigVersion.cmake
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

configure_package_config_file(${CMAKE_SOURCE_DIR}/cmake/Config.cmake.in
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_TYPES}Config.cmake
    INSTALL_DESTINATION cmake
)

install(FILES
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_TYPES}Config.cmake
    ${CMAKE_CURRENT_BINARY_DIR}/${MATH_TYPES}ConfigVersion.cmake
    DESTINATION cmake
)
